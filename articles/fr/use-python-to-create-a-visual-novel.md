---
title: Comment cr√©er un jeu de Visual Novel en 10 minutes - Tutoriel Python Ren'Py
subtitle: ''
author: Lynn Zheng
co_authors: []
series: null
date: '2021-06-22T16:18:29.000Z'
originalURL: https://freecodecamp.org/news/use-python-to-create-a-visual-novel
coverImage: https://www.freecodecamp.org/news/content/images/2021/06/Screen-Shot-2021-06-21-at-14.23.10-1.png
tags:
- name: Game Development
  slug: game-development
- name: Python
  slug: python
seo_title: Comment cr√©er un jeu de Visual Novel en 10 minutes - Tutoriel Python Ren'Py
seo_desc: 'Do you have a story idea that you''d like to turn into a novel? How about
  adding visual appeal and interactivity to that novel?

  A Visual Novel might be the game genre you are looking for. And this tutorial is
  here to help set you up in 10 minutes, wit...'
---

Avez-vous une id√©e d'histoire que vous aimeriez transformer en roman ? Et si vous ajoutiez un attrait visuel et de l'interactivit√© √† ce roman ?

Un [Visual Novel](https://en.wikipedia.org/wiki/Visual_novel) pourrait √™tre le genre de jeu que vous recherchez. Et ce tutoriel est l√† pour vous aider √† vous lancer en 10 minutes, avec une exp√©rience de codage minimale requise. Commen√ßons !

## Introduction et installation de l'outil

Nous allons utiliser [le moteur Ren'Py Visual Novel](https://www.renpy.org/), qui est construit sur Python 2.7. Comme Python lui-m√™me est un langage de script, vous pourrez "sc√©nariser" votre projet de visual novel dans Ren'Py.

> Depuis l'arriv√©e de Python 3, Python 2.7 a √©t√© abandonn√© et n'est plus activement maintenu. Soyez rassur√© - Python 2.7 poss√®de toutes les fonctionnalit√©s dont nous avons besoin pour cr√©er un visual novel g√©nial. De plus, la derni√®re version de Ren'Py, [Ren'Py SDK 7.4](https://www.renpy.org/release/7.4.0), offre un mode de compatibilit√© pour Python 3. Les d√©veloppeurs expriment √©galement l'espoir de s'int√©grer pleinement avec Python 3 dans la prochaine version, Ren'Py 8.0.

### Comment t√©l√©charger et installer Ren'Py

Vous pouvez t√©l√©charger la derni√®re version de Ren'Py pour votre syst√®me d'exploitation (Windows, Mac, Linux) sur [son site officiel](https://www.renpy.org/).

Une fois que vous avez t√©l√©charg√© et install√© Ren'Py, vous pouvez ouvrir le lanceur Ren'Py, s√©lectionner l'un des projets de d√©marrage (Tutorial, The Question) √† gauche, et cliquer sur **Lancer le projet**.

Consultez le **Tutoriel** pour avoir un aper√ßu de toute la puissance de ce moteur, ou **The Question** pour voir un visual novel tr√®s basique que vous pouvez cr√©er en 10 minutes.

![Image](https://www.freecodecamp.org/news/content/images/2021/06/Screen-Shot-2021-06-21-at-10.51.50.png align="left")

*Le lanceur Ren'Py*

## Comment cr√©er un nouveau projet dans Ren'Py

Cr√©ons un nouveau projet. J'ai appel√© le mien **Randonn√©e en for√™tüå≤**, mettant en sc√®ne une sc√®ne simple o√π deux enfants explorent un sentier forestier.

Faites attention √† la r√©solution que vous choisissez : Par d√©faut, elle est de 1280 x 720. Lorsque nous ajoutons des images, nos images de fond doivent √©galement respecter ces dimensions.

![Image](https://www.freecodecamp.org/news/content/images/2021/06/foresthike.gif align="left")

### Comment ex√©cuter le projet de base

Lancez le projet de base. Appuyez sur **D√©marrer** depuis le menu principal. Apr√®s deux br√®ves lignes de dialogue, le script se termine et nous sommes ramen√©s au menu principal.

![Image](https://www.freecodecamp.org/news/content/images/2021/06/launch.gif align="left")

*Ex√©cution du projet de base*

## Comment sc√©nariser notre projet

Commen√ßons √† sc√©nariser notre jeu bas√© sur le mod√®le.

Des √©diteurs de texte comme [Sublime Text](https://www.sublimetext.com/) et [Atom](https://atom.io/) ont tous deux une coloration syntaxique pour les scripts Ren'Py se terminant par `.rpy`. Consultez [ce package Sublime Text](https://packagecontrol.io/packages/Renpy%20Language) et [ce package Atom](https://atom.io/packages/language-renpy).

Les deux lignes de dialogue que nous avons vues se trouvent dans `script.rpy`. Ouvrez ce fichier et son contenu devrait ressembler √† ce qui suit. Tout comme en Python, les lignes qui commencent par `#` sont des commentaires et ne seront pas √©valu√©es comme faisant partie du script Ren'Py. Les commentaires et le code ci-dessous sont assez explicites.

```pgsql
# D√©clarer les personnages utilis√©s par ce jeu
define e = Character("Eileen")


# Le jeu commence ici
label start:

    # Afficher un fond
    scene bg room

    # Cela montre un sprite de personnage
    show eileen happy

    # Ces lignes affichent des dialogues.
    e "Vous avez cr√©√© un nouveau jeu Ren'Py."
    e "Une fois que vous ajoutez une histoire, des images et de la musique, vous pouvez le publier dans le monde !"

    # Cela termine le jeu.
    return
```

Le `label` est utilis√© pour le contr√¥le de flux, que nous aborderons dans la section suivante.

L'instruction `return` sur la derni√®re ligne est ce qui nous a ramen√©s au menu principal.

### Comment d√©clarer des personnages et ajouter des dialogues

Rempla√ßons la d√©claration de personnage et les dialogues de base par ceux de notre histoire. Voici comment se d√©roule mon histoire :

```pgsql
define laura = Character('Laura')
define tom = Character('Tom')

label start:

    laura "Attends-moi, Tom !"
    laura "Tom !"
    laura "J'ai dit attends-moi !"
    laura "...Tom ?"
    tom "Bouh !"
    laura "A√Øe... pas encore."
    tom "As-tu peur ?"
    laura "Pas du tout."
    laura "Partir comme √ßa est dangereux, tu sais."
    laura "Nous sommes dans la for√™t. Nous pourrions nous perdre."
    tom "D'accord, d'accord, maman. Je ne le referai plus."

    return
```

![Image](https://www.freecodecamp.org/news/content/images/2021/06/story.gif align="left")

### Comment ajouter des images et des transitions

Si vous n'√™tes pas artiste vous-m√™me, vous pouvez envisager de chercher des ressources dans le domaine cr√©atif commun. [itch.io](https://itch.io/), un march√© pour les jeux ind√©pendants, est un excellent endroit pour chercher des ressources.

J'ai trouv√© [cet ensemble de sprites de personnages](https://fuelli.itch.io/free-to-use-character-sprites) pour mon projet. Pour les images de fond, j'ai simplement appliqu√© des filtres artistiques √† des images cr√©atives communes, donnant aux images de la vie r√©elle une esth√©tique aquarelle agr√©able.

J'ai mis toutes mes images dans `game/images`. Notez qu'il est acceptable d'utiliser des espaces dans les noms de fichiers d'images.

![Image](https://www.freecodecamp.org/news/content/images/2021/06/Screen-Shot-2021-06-21-at-13.23.45.png align="left")

*Une mani√®re conventionnelle d'organiser les ressources d'images*

Ensuite, nous ajoutons √† `script.rpy` ces images ainsi que quelques transitions. Ren'Py applique des transitions lorsqu'il voit des mots-cl√©s comme `with` et `at`. Vous pouvez en lire plus sur les transitions dans [la documentation ATL (Animation and Transition Language) de Ren'Py](https://www.renpy.org/doc/html/atl.html).

```pgsql
label start:
    scene bg forest day with fade
    show laura angry
    laura "Attends-moi, Tom !"
    laura "Tom !"
    laura "J'ai dit attends-moi !"
    laura "...Tom ?"
    hide laura
    scene bg forest day with vpunch
    show tom happy at right with moveinbottom
    tom "Bouh !"
    show laura angry at left with moveinleft
    laura "A√Øe... pas encore."
    tom "As-tu peur ?"
    laura "Pas du tout."
    show laura sad
    laura "Partir comme √ßa est dangereux, tu sais."
    laura "Nous sommes dans la for√™t. Nous pourrions nous perdre."
    tom "D'accord, d'accord, maman. Je ne le referai plus."

    return
```

![Image](https://www.freecodecamp.org/news/content/images/2021/06/images.gif align="left")

Avec l'ajout de visuels, notre histoire prend forme agr√©ablement.

### Comment ajouter des choix

Un jeu avec diff√©rentes branches et fins double le plaisir. Ajouter un menu de choix √† un script Ren'Py est simple :

```pgsql
menu:
    "Quel chemin devons-nous prendre ?"

    "Gauche" :
        tom "Explorons le sentier de gauche !"
    "Droite" :
        tom "La droite est toujours la bonne direction !"
```

![Image](https://www.freecodecamp.org/news/content/images/2021/06/choice.gif align="left")

### Comment utiliser les variables Python et le contr√¥le de flux

Nous pouvons d√©finir des variables Python dans un script Ren'Py et modifier le flux de notre histoire en fonction de leurs valeurs. Les instructions Python commencent par un `$` ou un bloc `python:` indent√©.

Ajout de variables √† notre menu de choix pr√©c√©dent :

```pgsql
menu:
    "Quel chemin devons-nous prendre ?"

    "Gauche" :
        tom "Explorons le sentier de gauche !"
        $ is_lost = True
    "Droite" :
        tom "La droite est toujours la bonne direction !"
        $ is_lost = False
scene bg forest noon with Dissolve(3.0)
scene bg forest dusk with Dissolve(3.0)
show laura sad at left with moveinleft
laura "Il se fait tard. Es-tu s√ªr que nous ne sommes pas perdus ?"
if is_lost:
    show tom sad at right with moveinleft
    tom "J'esp√®re que non, mais j'ai un mauvais pressentiment."
else:
    show tom happy at right with moveinleft
    tom "Nous allons bien. Regarde ! Il y a la fin du sentier."
    tom "Je suis le meilleur √©claireur des environs."
```

![Image](https://www.freecodecamp.org/news/content/images/2021/06/ezgif.com-gif-maker-2-.gif align="left")

Voir la fin de cet article pour mes ressources artisanales pour travailler avec Python dans Ren'Py.

### Comment jouer de la musique

Selon [la documentation audio de Ren'Py](https://www.renpy.org/doc/html/audio.html), jouer de la musique et des effets sonores est aussi simple que ce qui suit :

```pgsql
play music "mozart.ogg"
play sound "woof.mp3"
```

### Comment sauvegarder et charger le jeu

Ren'Py a fait tout le travail difficile pour nous et dispose d'un syst√®me de sauvegarde et de chargement int√©gr√©.

![Image](https://www.freecodecamp.org/news/content/images/2021/06/save.gif align="left")

*Sauvegarder le jeu*

![Image](https://www.freecodecamp.org/news/content/images/2021/06/load.gif align="left")

*Charger le jeu*

### Autres personnalisations que vous pouvez faire

Actuellement, dans notre dialogue, toute la ligne de texte est affich√©e en une fois, au lieu de caract√®re par caract√®re. Nous pouvons changer la variable `preference.text_cps` (CPS signifie caract√®res par seconde) dans `options.rpy` comme ceci.

```pgsql
default preferences.text_cps = 20
```

![Image](https://www.freecodecamp.org/news/content/images/2021/06/cps.gif align="left")

*D√©finir un CPS personnalis√© affiche le texte caract√®re par caract√®re √† un rythme donn√©*

Il y a encore plus que nous pouvons personnaliser dans `gui.rpy` (GUI signifie Interface Graphique Utilisateur, qui inclut la bo√Æte de texte et les √©l√©ments de menu de choix que nous avons vus) ou `screens.rpy`.

## Que peut faire d'autre Ren'Py ?

Les capacit√©s de Ren'Py vont bien au-del√† de l'affichage de texte et d'images. Je pourrais aller jusqu'√† dire que Ren'Py est aussi capable et polyvalent que Python lui-m√™me.

Avec le module [Pygame](https://www.pygame.org/news), il est possible de cr√©er des mini-jeux complexes dans Ren'Py. J'ai moi-m√™me cr√©√© et ouvert plusieurs mini-jeux, dont un moteur d'√©checs qui s'int√®gre avec l'IA d'√©checs Stockfish ainsi qu'un moteur de jeu de rythme qui g√©n√®re automatiquement la carte de rythme pour tout fichier musical.

![Image](https://www.freecodecamp.org/news/content/images/2021/06/promotion.gif align="left")

*Ma d√©monstration de jeu d'√©checs*

![Image](https://www.freecodecamp.org/news/content/images/2021/06/demo-4.gif align="left")

*Ma d√©monstration de jeu de rythme*

%[https://r3dhummingbird.itch.io/renpy-chess-game]

%[https://r3dhummingbird.itch.io/renpy-rhythm-game]

## Ressources

Ce tutoriel devrait vous aider √† commencer avec Ren'Py. Il est toujours utile de se r√©f√©rer √† [la documentation officielle](https://www.renpy.org/doc/html/) lorsque vous apprenez les fonctionnalit√©s plus avanc√©es pour ajouter du dynamisme √† votre projet.

J'ai √©galement cr√©√© du mat√©riel de cours pour vous aider √† vous rafra√Æchir sur les fondamentaux de Python et leurs capacit√©s dans les scripts Ren'Py.

%[https://github.com/RuolinZheng08/python-for-renpy-dev]

%[https://www.udemy.com/course/python-basics-for-renpy-developers/?referralCode=774C55606994052EBFCB]

Consultez ma vid√©o d'introduction au cours sur YouTube :

%[https://www.youtube.com/watch?v=pQcb_pfIbI0]

Merci d'avoir lu et amusez-vous √† raconter votre histoire !