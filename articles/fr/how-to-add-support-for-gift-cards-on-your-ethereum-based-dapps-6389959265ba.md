---
title: Comment ajouter la prise en charge des cartes-cadeaux sur vos dapps bas√©es
  sur Ethereum
subtitle: ''
author: freeCodeCamp
co_authors: []
series: null
date: '2017-12-28T13:21:14.000Z'
originalURL: https://freecodecamp.org/news/how-to-add-support-for-gift-cards-on-your-ethereum-based-dapps-6389959265ba
coverImage: https://cdn-media-1.freecodecamp.org/images/1*i0MghtjGDTM7j0Wlaj9JfA.jpeg
tags:
- name: Blockchain
  slug: blockchain
- name: General Programming
  slug: programming
- name: software development
  slug: software-development
- name: 'tech '
  slug: tech
- name: Web Development
  slug: web-development
seo_title: Comment ajouter la prise en charge des cartes-cadeaux sur vos dapps bas√©es
  sur Ethereum
seo_desc: 'By Pablo Ruiz

  Ahhh, Christmas‚Ä¶ That magical time of the year where you have to buy presents for
  all your loved ones and you don‚Äôt know what to get them‚Ä¶

  Gift Cards are the perfect option for those who don‚Äôt know what to buy for someone
  for the Holida...'
---

Par Pablo Ruiz

Ahhh, No√´l‚Ä¶ Cette p√©riode magique de l'ann√©e o√π vous devez acheter des cadeaux pour tous vos proches et vous ne savez pas quoi leur offrir‚Ä¶

Les cartes-cadeaux sont l'option parfaite pour ceux qui ne savent pas quoi acheter pour quelqu'un pendant les f√™tes, leur anniversaire ou une autre occasion sp√©ciale.

Faire en sorte que votre boutique accepte les cartes-cadeaux offre une tr√®s bonne fa√ßon de stimuler les ventes et permet r√©ellement des achats en cha√Æne ¬´ au nom ¬ª de quelqu'un d'autre.

Vous ne savez pas quels Cryptokitties acheter pour vos proches ? Pourquoi ne pas leur offrir une carte-cadeau et les laisser acheter ceux qu'ils veulent vraiment ? (_Bien s√ªr, cela ne fonctionnerait pas directement car les contrats intelligents Cryptokitties devraient √™tre modifi√©s pour accepter les cartes-cadeaux._ ü§î)

Dans cet article, je vais expliquer comment √©mettre des cartes-cadeaux en cha√Æne et comment les accepter dans vos propres contrats intelligents/dapps. Pour cela, nous allons cr√©er un contrat intelligent GiftCardIssuer que vos propres contrats intelligents pourront h√©riter pour travailler avec des cartes-cadeaux.

### Comment fonctionnent les cartes-cadeaux

L'id√©e derri√®re ce contrat intelligent est de fournir la logique n√©cessaire √† vos propres contrats qui re√ßoivent des paiements afin qu'ils puissent accepter une carte-cadeau avec un solde pr√©pay√© au lieu de ¬´ cash ¬ª.

N'importe qui pourrait √©mettre une nouvelle carte-cadeau qui n'est valable que pour les achats sur votre contrat intelligent, et uniquement valable pour la personne que le compte √©metteur choisit comme b√©n√©ficiaire.

√âmettre une carte-cadeau dans une boutique compatible serait tr√®s facile et simple. Le compte souhaitant offrir une carte-cadeau doit simplement appeler la fonction correspondante sur votre contrat intelligent, fournir un identifiant pour la carte, s√©lectionner le compte b√©n√©ficiaire et payer pour celle-ci.

Notre contrat intelligent GiftCardIssuer g√©n√©rera ensuite la carte-cadeau selon les param√®tres et les r√®gles commerciales que nous avons pr√©d√©finis. Par exemple, nous pouvons faire en sorte que les cartes-cadeaux g√©n√©r√©es par nos contrats intelligents n'acceptent qu'un montant minimum de financement √† fournir, ou nous pourrions les rendre rechargeables.

### D√©veloppement du contrat intelligent GiftCardIssuer

Vous pouvez consulter le code complet, enti√®rement comment√© et l'exemple d'impl√©mentation sur mon [d√©p√¥t Github](https://github.com/pabloruiz55/GiftCardIssuer).

Dans les paragraphes suivants, je vais passer en revue les aspects les plus importants du contrat intelligent GiftCardIssuer.

#### Structure d'une carte-cadeau

Voici √† quoi ressemblerait une carte-cadeau √©mise par nos contrats intelligents :

```
struct Card {
    uint value;
    uint issueDate;
    uint validThru;
    address beneficiary;
    address generatedBy;
    bool rechargeable;
    bool transfereable;
}
```

La structure ci-dessus d√©finit les propri√©t√©s de base des cartes-cadeaux que nous allons √©mettre, et nous aide √† d√©finir et √† appliquer les r√®gles commerciales que nous d√©finissons pour elles plus tard.

#### D√©finition des r√®gles commerciales pour les cartes-cadeaux

Les cartes-cadeaux que nous cr√©ons auront certaines r√®gles commerciales cod√©es en elles. Voici les r√®gles que j'ai d√©finies, mais d'autres pourraient √™tre ajout√©es en fonction des besoins de la boutique :

```
// Variables des r√®gles commerciales des cartes
uint public rule_Duration = 365 days;
bool public rule_Rechargeable = false;
uint public rule_MinValue = 1 wei;
uint public rule_MaxValue = 100 ether;
bool public rule_Transfereable = true;
```

* `**rule_Duration**` d√©finit la date d'expiration de la carte-cadeau
* `**rule_Rechargeable**` d√©finit si des fonds peuvent √™tre ajout√©s √† la carte ou non
* `**rule_Transfereable**` d√©finit si la carte peut √™tre donn√©e √† quelqu'un d'autre une fois √©mise
* `**rule_MinValue**` et `**rule_MaxValue**` d√©finissent le financement minimum et maximum que l'√©metteur peut ajouter √† la carte

Ces r√®gles commerciales peuvent √™tre modifi√©es √† la vol√©e par le propri√©taire de la boutique en utilisant la fonction `**setGiftCardRules()**`, mais gardez √† l'esprit que les modifications **ne s'appliquent** qu'aux nouvelles cartes-cadeaux √©mises. Les cartes d√©j√† √©mises conserveront les r√®gles avec lesquelles elles ont √©t√© √©mises.

```
function setGiftCardRules(
    bool _rechargeable,
    bool _transfereable,
    uint _duration,
    uint _minValue,
    uint _maxValue
) public {
    require(msg.sender == owner);
    require(duration >= 1 days);
    require(_minValue > 0);
    require(_maxValue >= _minValue);

    rule_Rechargeable = _rechargeable;
    rule_Transfereable = _transfereable;
    rule_Duration = _duration;
    rule_MinValue = _minValue;
    rule_MaxValue = _maxValue;
}
```

#### √âmission d'une nouvelle carte-cadeau

Les utilisateurs peuvent √©mettre de nouvelles cartes-cadeaux en appelant la fonction **payable** `**issueGiftCard()**` qui prend 2 param√®tres :

* `**_cardId**` : qui est une cha√Æne de 32 octets **unique** au choix de l'√©metteur (ils pourraient cr√©er une carte dont l'identifiant est ¬´ JOYEUX ANNIVERSAIRE, JEAN ! ¬ª)
* `**_beneficiary**` : qui est le compte qui pourra utiliser la carte-cadeau √©mise.

Lorsque la fonction est ex√©cut√©e, une nouvelle carte-cadeau sera √©mise (avec les r√®gles commerciales pr√©c√©demment d√©finies) au nom du b√©n√©ficiaire et avec les fonds envoy√©s avec l'appel de la fonction.

```
function issueGiftCard(bytes32 _cardId, address _beneficiary) public payable {
    require(msg.value > 0);
    require(cards[_cardId].issueDate == 0);
    require(msg.value >= rule_MinValue);
    require(msg.value <= rule_MaxValue);

    cards[_cardId].value = msg.value;
    cards[_cardId].beneficiary = _beneficiary;
    cards[_cardId].generatedBy = msg.sender;
    cards[_cardId].issueDate = now;
    cards[_cardId].validThru = now + rule_Duration;
    cards[_cardId].rechargeable = rule_Rechargeable;
    cards[_cardId].transfereable = rule_Transfereable;

    // ajouter de la valeur au solde du commer√ßant
    balance += msg.value;

    E_GiftCardIssued(_cardId, now, msg.sender, _beneficiary, msg.value);
}
```

#### Traitement des cartes-cadeaux

Pour accepter un paiement par carte-cadeau, le contrat intelligent de la boutique devrait impl√©menter une fonction qui appelle la fonction `**useGiftCard()**` de GiftCardIssuer :

```
function useGiftCard(bytes32 _cardId, uint _prodPrice) public returns (bool) {
    // La carte-cadeau ne peut √™tre utilis√©e que par le compte auquel elle a √©t√© √©mise
    require(msg.sender == cards[_cardId].beneficiary);
    // la carte doit exister
    require(cards[_cardId].issueDate > 0);
    // La carte ne doit pas avoir expir√©
    require(now <= cards[_cardId].validThru);
    // La carte doit avoir suffisamment de fonds pour couvrir l'achat
    require(cards[_cardId].value >= _prodPrice);
    // retirer la valeur du solde de la carte
    cards[_cardId].value -= _prodPrice;

    E_GiftCardUsed(_cardId, now, cards[_cardId].beneficiary, _prodPrice);
    return (true);
}
```

La fonction ci-dessus serait appel√©e depuis le contrat intelligent de la boutique (qui h√©rite de GiftCardIssuer) comme ceci :

```
function buyWithGiftCard(bytes32 _cardId) public {
    // Essayer d'acheter le produit avec la carte-cadeau fournie
    require(useGiftCard(_cardId, itemPrice));

    itemsBought[msg.sender]++;
}
```

`**buyWithGiftCard()**` accepte un `_cardId` et appelle sa fonction `useGiftCard` en passant le `_cardId` et `itemPrice`. C'est le prix du produit qui sera achet√© (et repr√©sente combien d'ether nous allons soustraire du solde de la carte-cadeau).

`**useGiftCard()**` proc√®de √† la v√©rification que la carte-cadeau est valide et, si c'est le cas, soustrait le solde et accepte le paiement. Si la carte-cadeau n'est pas valide ou n'a pas suffisamment de fonds, la fonction √©chouera et toute la transaction √©chouera.

#### Accepter les paiements avec des cartes-cadeaux

Voici un exemple d'impl√©mentation d'un contrat intelligent de boutique extr√™mement simple qui accepte √† la fois l'ether ou les cartes-cadeaux :

```
contract Store is GiftCardIssuer {
    uint itemPrice = 1 ether;
    mapping(address => uint) public itemsBought;

    function buyWithGiftCard(bytes32 _cardId) public {
        // Essayer d'acheter le produit avec la carte-cadeau fournie
        require(useGiftCard(_cardId, itemPrice));

        itemsBought[msg.sender]++;
    }

    function buyWithEther() public payable {
        require(msg.value == itemPrice);

        itemsBought[msg.sender]++;
    }
}
```

Un contrat intelligent souhaitant √©mettre des cartes-cadeaux et les accepter au lieu de l'ether devrait simplement h√©riter du contrat de base GiftCardIssuer et appeler correctement `**require(useGiftCard(_cardId, itemPrice));**`.

#### Recharger et transf√©rer les cartes-cadeaux

Comme d√©fini dans la section des r√®gles commerciales des cartes-cadeaux, nous pouvons configurer les cartes-cadeaux pour qu'elles soient ¬´ rechargeables ¬ª et/ou ¬´ transf√©rables ¬ª.

**Une carte-cadeau transf√©rable** peut √™tre donn√©e √† un autre compte par le compte actuellement d√©fini comme b√©n√©ficiaire. Tout ce qu'ils ont √† faire est d'appeler la fonction suivante (tant que les r√®gles de la carte-cadeau le permettent) :

```
function transferGiftCardTo(bytes32 _cardId, address _newBeneficiary) public {
    require(msg.sender == cards[_cardId].beneficiary);
    require(cards[_cardId].transfereable);
    require(cards[_cardId].issueDate > 0);
    require(_newBeneficiary != address(0));

    cards[_cardId].beneficiary = _newBeneficiary;
}
```

Une carte-cadeau rechargeable peut avoir des fonds ajout√©s. N'importe qui peut appeler la fonction suivante pour ajouter des fonds √† une carte-cadeau existante (tant que les r√®gles de la carte-cadeau le permettent) :

```
function addFundsToGiftCard(bytes32 _cardId) public payable {
    require(cards[_cardId].rechargeable);
    require(msg.value > 0);
    require(cards[_cardId].issueDate > 0);
    require(msg.value >= rule_MinValue);
    require(msg.value <= rule_MaxValue);

    cards[_cardId].value += msg.value;
    cards[_cardId].validThru = now + rule_Duration; // √âtendre la dur√©e

    // ajouter de la valeur au solde du commer√ßant
    balance += msg.value;
}
```

### Joyeux don de cadeaux !

Il y a beaucoup de nouvelles r√®gles et propri√©t√©s qui pourraient √™tre ajout√©es aux cartes-cadeaux. Il y a aussi la possibilit√© de cr√©er un √©metteur universel de cartes-cadeaux qui g√©n√®re des cartes-cadeaux non seulement valables dans une boutique, mais dans n'importe quelle boutique faisant partie du r√©seau de l'√©metteur.

_Je esp√®re que vous avez appr√©ci√© la lecture de cet article autant que j'ai appr√©ci√© l'√©crire. Je prends actuellement des missions de conseil li√©es au d√©veloppement de contrats intelligents. Si vous pr√©voyez de lever des fonds via un ICO ou de construire un produit bas√© sur la Blockchain, n'h√©sitez pas √† me contacter._