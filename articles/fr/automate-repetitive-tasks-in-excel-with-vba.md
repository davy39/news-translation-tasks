---
title: Tutoriel Visual Basic Excel ‚Äì Comment automatiser les t√¢ches r√©p√©titives dans
  une feuille de calcul
subtitle: ''
author: freeCodeCamp
co_authors: []
series: null
date: '2021-07-29T15:35:00.000Z'
originalURL: https://freecodecamp.org/news/automate-repetitive-tasks-in-excel-with-vba
coverImage: https://www.freecodecamp.org/news/content/images/2021/07/Thumbnail2.png
tags:
- name: automation
  slug: automation
- name: excel
  slug: excel
- name: visual basic
  slug: visual-basic
seo_title: Tutoriel Visual Basic Excel ‚Äì Comment automatiser les t√¢ches r√©p√©titives
  dans une feuille de calcul
seo_desc: "By Sander Vreeken\nI use VBA, or Visual Basic for Applications, to automate\
  \ my repetitive tasks. This saves me quite a lot of time in my day-to-day life.\
  \ \nBased on the fact that you're reading this tutorial, I assume that you would\
  \ like to be able to ..."
---

Par Sander Vreeken

J'utilise VBA, ou Visual Basic pour Applications, pour automatiser mes t√¢ches r√©p√©titives. Cela me fait gagner beaucoup de temps dans ma vie quotidienne. 

√âtant donn√© que vous lisez ce tutoriel, je suppose que vous aimeriez pouvoir faire de m√™me.

Je vais donc vous guider √† travers les bases de VBA, comme l'utilisation de variables, de boucles, de structures if-else, de tableaux et de dictionnaires.

## Introduction √† VBA et aper√ßu du projet

Bien que VBA ait √©t√© d√©clar√© obsol√®te en 2008, cette impl√©mentation de Visual Basic peut vous aider √† automatiser les t√¢ches r√©p√©titives de votre vie quotidienne.

Le langage est orient√© objet, il est √©crit en C++, et il inclut toutes les fonctionnalit√©s que vous attendez d'un langage de programmation de nos jours.

Dans ce tutoriel, nous allons √©crire une macro qui pr√©pare un mod√®le pour noter les temp√©ratures dans plusieurs villes europ√©ennes ‚Äì Amsterdam, Barcelone, Berlin, Bruxelles, Londres et Rome. 

Le mod√®le sera automatiquement cr√©√© par une macro que nous construirons ensemble en fonction des villes et de la date que nous utiliserons comme param√®tres. 

Enfin, nous apprendrons √©galement comment importer les donn√©es depuis un autre fichier dans notre mod√®le.

Pour suivre ce tutoriel, je m'attends √† ce que vous ayez une compr√©hension de base d'Excel. Mais vous n'avez besoin d'aucune exp√©rience avec VBA (bien que cela puisse vous aider √† comprendre les diff√©rents concepts que je vais introduire).

J'ai t√©l√©charg√© [plusieurs fichiers Excel de donn√©es](https://www.meteoblue.com/) qui sont libres d'utilisation. Vous pouvez √©galement [les trouver sur mon GitHub](https://github.com/SanderVreeken/FreeCodeCamp-VBA-Automation) avec le r√©sultat final.

## D√©finissons nos param√®tres

Tout d'abord, nous allons d√©finir les diff√©rentes villes que nous utiliserons pour ce tutoriel. Si nous devions faire cela manuellement, augmenter le nombre de villes signifierait cons√©quemment plus de travail. 

Mais dans notre cas, puisque la macro fera le travail pour nous, nous pouvons ajouter autant de villes que nous le souhaitons. Ensuite, nous ex√©cuterons simplement la macro et irons faire autre chose.

Pour d√©finir les villes, nous pouvons faire l'une des deux choses suivantes. Soit nous pouvons inclure les villes dans un tableau dans le code, soit nous pouvons les d√©finir dans une feuille de calcul s√©par√©e dans notre mod√®le. 

Pour ce tutoriel, nous allons faire cette derni√®re option, ce qui facilite l'ajout de villes par d'autres personnes sans aucune connaissance de VBA.

### Comment configurer le classeur Excel

Ouvrez un nouveau fichier Excel, enregistrez-le en tant que Classeur Excel avec macros (extension .xlsm), et nommez-le comme vous le souhaitez. 

Renommez la premi√®re feuille de calcul en Villes et ajoutez les six villes en tapant leurs noms dans la premi√®re colonne avec une ville sur chaque ligne, comme montr√© ici :

![Image](https://www.freecodecamp.org/news/content/images/2021/07/image-83.png)
_Figure 01 : Feuille de calcul Villes_

Pour d√©finir la date, nous allons cr√©er une autre feuille de calcul appel√©e Date o√π l'utilisateur peut d√©finir le mois pour lequel il souhaite que le mod√®le soit cr√©√©. 

Puisque toutes les ann√©es et tous les mois ne sont pas identiques (par exemple, nombre diff√©rent de jours, nombre diff√©rent de jours de semaine), le mod√®le pour vos donn√©es doit √™tre ajust√© chaque mois afin d'afficher correctement ces diff√©rences. 

J'ai √©galement ajout√© une validation des donn√©es pour m'assurer que nous ne pouvons entrer qu'une date et pour m'assurer qu'elle ne d√©passe pas la date d'aujourd'hui (mais cela est compl√®tement facultatif). Tant que vous avez une cellule avec une date, de pr√©f√©rence la cellule B1, cela suffit.

Pour ce tutoriel, nous n'aurons besoin que du mois et de l'ann√©e, j'ai donc choisi un format de date diff√©rent comme montr√© ci-dessous :

![Image](https://www.freecodecamp.org/news/content/images/2021/07/image-89.png)
_Figure 02 : Feuille de calcul Date_

## Comment cr√©er une macro avec Visual Basic pour Applications

Maintenant que nous avons d√©fini les param√®tres, nous pouvons passer √† la cr√©ation de la macro.

Si c'est la premi√®re fois que vous utilisez VBA, vous devrez peut-√™tre personnaliser votre ruban pour obtenir les fonctions n√©cessaires.

Pour ce faire, assurez-vous que la case D√©veloppeur est coch√©e (comme montr√© dans la Figure 3 ci-dessous) et que les options D√©veloppeur sont disponibles dans votre ruban apr√®s avoir enregistr√© vos modifications.

![Image](https://www.freecodecamp.org/news/content/images/2021/07/image-85.png)
_Figure 03 : Param√®tres Excel_

Vous devriez maintenant pouvoir ouvrir Visual Basic depuis l'onglet D√©veloppeur dans Excel, ce qui devrait ressembler √† ceci :

![Image](https://www.freecodecamp.org/news/content/images/2021/07/image-86.png)
_Figure 04 : Visual Basic_

C'est l'√©diteur fourni par Excel o√π vous pourrez cr√©er, ajuster et supprimer vos fonctions et macros. Je ne vais pas entrer dans trop de d√©tails pour l'instant, mais je vais expliquer certains des √©l√©ments au fur et √† mesure.

Maintenant, mettons les mains dans le cambouis et √©crivons notre premi√®re macro.   
  
Vous pourriez choisir d'√©crire des macros pour une seule feuille de calcul, ou choisir de les avoir disponibles dans tout le classeur. 

Comme les deux feuilles de calcul que nous avons cr√©√©es pr√©c√©demment ne maintiennent que les param√®tres, j'ai choisi d'√©crire les macros pour tout le classeur en double-cliquant sur l'option "ThisWorkbook" dans la barre lat√©rale de notre projet. 

_Ne vous inqui√©tez pas pour le PERSONAL.xlsb dans ma capture d'√©cran pour l'instant ‚Äì c'est un fichier contenant des fonctions que je peux utiliser dans tous mes fichiers et qui sera trait√© dans un futur tutoriel._

### Comment cr√©er notre premi√®re macro

Apr√®s avoir s√©lectionn√© le classeur, vous √™tes pr√™t √† commencer votre premier programme.

Les macros en VBA commencent par le mot-cl√© Sub, abr√©viation de subroutine, suivi de leur nom et de deux parenth√®ses. Bien que l'√©diteur ne soit rien compar√© √† un IDE comme Visual Studio Code, il compl√©tera le code avec End Sub lorsque vous appuierez sur Entr√©e apr√®s les deux parenth√®ses. 

Pour l'instant, j'ai cr√©√© une macro vide appel√©e `CreateTemplate` qui ressemble √† ceci :

```vba
Sub CreateTemplate()

End Sub
```

Il est un peu triste que la fonction ne fasse rien pour l'instant, alors ajoutons le code suivant et voyons ce qu'il fait :

```vba
Sub CreateTemplate()

    'Ajout d'une nouvelle feuille de calcul appel√©e NewSheet au classeur, √† c√¥t√© de la feuille de calcul actuellement s√©lectionn√©e.
    Sheets.Add.Name = "NewSheet"
    
End Sub
```

Si nous ex√©cutions ce code maintenant, la fonction cr√©erait une nouvelle feuille de calcul dans notre classeur Excel appel√©e NewSheet. 

Notez que j'ai inclus un commentaire dans le code en commen√ßant la ligne par une apostrophe. Cela ne sera pas ex√©cut√©, mais est seulement l√† pour vous aider, vous et moi, √† comprendre le code. 

Nous pouvons ex√©cuter ce code en pla√ßant notre curseur quelque part dans la fonction et en appuyant sur l'ic√¥ne verte 'play' en haut de l'√©diteur, qui dit Run Sub lorsque vous passez la souris dessus. 

Apr√®s avoir appuy√© sur ce bouton, vous verrez qu'une nouvelle feuille de calcul appel√©e NewSheet a √©t√© cr√©√©e dans notre classeur et a √©galement √©t√© ajout√©e dans la barre lat√©rale, √† c√¥t√© des feuilles que nous avions d√©j√†.

Personnellement, je n'aime pas le fait que la feuille soit cr√©√©e √† c√¥t√© de la feuille que nous avions (peut-√™tre intentionnellement, peut-√™tre pas) s√©lectionn√©e. Par cons√©quent, je vais ajouter un param√®tre √† la m√©thode add pour d√©finir son emplacement :

```vba
Sub CreateTemplate()
    
    'Ajout d'une nouvelle feuille de calcul appel√©e NewSheet au classeur, √† c√¥t√© de la feuille de calcul Date.
    Sheets.Add(After:=Sheets("Date")).Name = "NewSheet"
    
End Sub
```

Supprimez la feuille nouvellement cr√©√©e, car nous allons maintenant cr√©er les feuilles de calcul pour chaque ville que nous avons d√©finie pr√©c√©demment. Comme le nombre de villes entr√©es peut diff√©rer, nous voulons savoir combien de lignes sont r√©ellement utilis√©es dans notre feuille de calcul Villes.

```vba
Sub CreateTemplate()

    Debug.Print Worksheets("Cities").UsedRange.Rows.Count
    
End Sub
```

Pour tester si nous sommes capables d'extraire la date du fichier, nous utilisons `Debug.Print` (similaire √† print en Python ou console.log en JavaScript) pour imprimer le nombre de lignes, que Excel calculera pour nous en fonction du code que nous avons fourni.

Assurez-vous d'ouvrir votre Fen√™tre Imm√©diate (dans Visual Basic, en s√©lectionnant Affichage > Fen√™tre Imm√©diate) et ex√©cutez la macro ci-dessus. Elle imprimera six, comme nous nous y attendons, apr√®s avoir d√©fini le m√™me nombre de villes dans notre feuille de calcul Villes plus t√¥t dans ce tutoriel.

## Comment stocker des valeurs en tant que variables dans VBA

Plut√¥t que d'imprimer cette valeur, je veux la stocker en tant que variable. Pour ce faire, ajoutez le code suivant :

```vba
Sub CreateTemplate()
    'Variable qui contiendra le nombre de villes.
    Dim NumberOfCities As Integer
    
    NumberOfCities = Worksheets("Cities").UsedRange.Rows.Count
    Debug.Print NumberOfCities
    
End Sub
```

Nous utilisons le mot-cl√© Dim en VBA pour d√©clarer une variable, qui doit toujours inclure le type (par exemple String, Integer, Double). 

Apr√®s avoir d√©clar√© notre variable, nous pouvons assigner le nombre de lignes utilis√©es, comme imprim√© pr√©c√©demment, √† cette variable et imprimer le nombre pour tester que nous obtenons le m√™me r√©sultat. Vous pouvez voir cela dans l'image ci-dessous :

![Image](https://www.freecodecamp.org/news/content/images/2021/07/image-90.png)
_Figure 05 : Impression dans la Fen√™tre Imm√©diate_

Nous pouvons maintenant utiliser ce nombre pour d√©finir une boucle qui cr√©era une feuille pour chaque ville.

```vba
Sub CreateTemplate()
    'Variable qui contiendra le nom de la ville utilis√© pour nommer une feuille.
    Dim CityName As String
    'Variable qui contiendra le nombre de villes.
    Dim NumberOfCities As Integer
    'Variable qui sera utilis√©e pour suivre l'index de la boucle pour cr√©er des feuilles de calcul.
    Dim SheetIndex As Integer
    
    NumberOfCities = Worksheets("Cities").UsedRange.Rows.Count
    
    For SheetIndex = 1 To NumberOfCities
        'D√©finition de la variable cityname bas√©e sur l'index.
        CityName = Worksheets("Cities").Cells(SheetIndex, 1)
        'Ajout d'une nouvelle feuille de calcul appel√©e NewSheet au classeur en dernier pour assurer l'ordre alphab√©tique.
        Sheets.Add(After:=Sheets(Sheets.Count)).Name = CityName
    Next SheetIndex
    
End Sub
```

## Comment travailler avec des boucles en VBA

Permettez-moi d'expliquer comment tout cela fonctionne. Dans l'exemple ci-dessus, j'ai d√©clar√© deux variables suppl√©mentaires, √† savoir `CityName` et `SheetIndex`, qui contiendront le nom de la ville (que nous utiliserons pour le titre de la feuille) et maintiendront l'index de la boucle.

La boucle elle-m√™me commence par le mot-cl√© `For`. Par la suite, nous d√©finissons le d√©but de l'index (un dans ce cas) et parcourons jusqu'√† ce que le nombre de villes soit atteint. 

Pour chaque it√©ration, la ville a √©t√© stock√©e dans la variable `CityName`, que nous utilisons ensuite pour cr√©er une nouvelle feuille de calcul. Ex√©cutez la fonction et vous verrez les feuilles de calcul √™tre cr√©√©es avec le titre tel que d√©fini dans notre feuille de calcul Villes. C'est magique, je sais !

Maintenant que les feuilles de calcul sont l√†, nous pouvons ajouter les dates individuelles pour le mois. Plus t√¥t, nous avons d√©j√† d√©fini le mois que nous voulons utiliser dans la feuille de calcul Date. Cela peut maintenant nous aider √† cr√©er une autre boucle afin que tous les jours du mois soient repr√©sent√©s.

## Comment utiliser des fonctions en VBA

Mais d'abord, nous devons obtenir le nombre de jours du mois que l'utilisateur a entr√© dans l'onglet Date. Nous pouvons faire cela en utilisant une fonction en VBA. Plut√¥t que les sous-routines que nous avons utilis√©es pr√©c√©demment, nous pouvons √©galement utiliser une fonction qui retourne quelque chose. 

La fonction que nous utilisons maintenant ressemble beaucoup √† la fonction que vous pourriez utiliser dans Excel lorsque vous d√©finissez le nombre de jours dans un mois (mais vous utiliseriez `Date` au lieu de `DateSerial`) :

```vba
Function DaysInMonth(DateInput As Date)
    DaysInMonth = Day(DateSerial(Year(DateInput), Month(DateInput) + 1, 1) - 1)
End Function
```

Vous pouvez entrer cette fonction soit au-dessus, soit en dessous de la sous-routine que nous avons d√©finie pr√©c√©demment, car VBA est un langage compil√© plut√¥t qu'interpr√©t√©.

_Note ‚Äì bien que je ne collerai pas la fonction √† nouveau, elle sera l√† pour le reste de ce tutoriel._

Nous pouvons maintenant utiliser cette fonction dans notre sous-routine. Cela facilite beaucoup notre vie, car nous pouvons maintenant utiliser ces superpouvoirs sans polluer notre sous-routine. 

```
Sub CreateTemplate()
    'Variable qui contiendra le nom de la ville utilis√© pour nommer une feuille.
    Dim CityName As String
    Dim MonthNum As Integer
    'Variable qui contiendra le nombre de villes.
    Dim NumberOfCities As Integer
    'Variable qui contiendra la date entr√©e dans la feuille de calcul Date.
    Dim ReportDate As Date
    'Variable qui sera utilis√©e pour suivre l'index de la boucle pour cr√©er des feuilles de calcul.
    Dim SheetIndex As Integer
    
    NumberOfCities = Worksheets("Cities").UsedRange.Rows.Count
    
    For SheetIndex = 1 To NumberOfCities
        'D√©finition de la variable cityname bas√©e sur l'index.
        CityName = Worksheets("Cities").Cells(SheetIndex, 1)
        'Ajout d'une nouvelle feuille de calcul appel√©e NewSheet au classeur en dernier pour assurer l'ordre alphab√©tique.
        Sheets.Add(After:=Sheets(Sheets.Count)).Name = CityName
    Next SheetIndex
    
    ReportDate = Worksheets("Date").Cells(1, 2)
    Debug.Print DaysInMonth(ReportDate)
    
End Sub
```

Pour l'instant, supprimez les feuilles de calcul avec les noms de villes si vous les aviez cr√©√©es et ex√©cutez la macro (√† nouveau) pour vous assurer que vous avez obtenu le nombre correct de jours imprim√© dans la Fen√™tre Imm√©diate pour le mois que vous avez d√©fini pr√©c√©demment dans la feuille de calcul Date.

Nous pouvons ensuite stocker le nombre de jours en tant qu'entier dans une autre variable (appel√©e `NumberOfDays`) pour une autre boucle. 

```
Sub CreateTemplate()
    'Variable qui contiendra le nom de la ville utilis√© pour nommer une feuille.
    Dim CityName As String
    'Variable qui sera utilis√©e pour suivre l'index de la boucle pour ajouter les dates individuelles pour le mois.
    Dim DateIndex As Integer
    'Variable qui contiendra le nombre de villes.
    Dim NumberOfCities As Integer
    'Variable qui contiendra le nombre de jours dans le mois choisi
    Dim NumberOfDays As Integer
    'Variable qui contiendra la date entr√©e dans la feuille de calcul Date.
    Dim ReportDate As Date
    'Variable qui sera utilis√©e pour suivre l'index de la boucle pour cr√©er des feuilles de calcul.
    Dim SheetIndex As Integer
    
    NumberOfCities = Worksheets("Cities").UsedRange.Rows.Count
    
    For SheetIndex = 1 To NumberOfCities
        'D√©finition de la variable cityname bas√©e sur l'index.
        CityName = Worksheets("Cities").Cells(SheetIndex, 1)
        'Ajout d'une nouvelle feuille de calcul appel√©e NewSheet au classeur en dernier pour assurer l'ordre alphab√©tique.
        Sheets.Add(After:=Sheets(Sheets.Count)).Name = CityName
    Next SheetIndex
    
    ReportDate = Worksheets("Date").Cells(1, 2)
    NumberOfDays = DaysInMonth(ReportDate)
    
    For SheetIndex = 1 To NumberOfCities
        'D√©finition de la variable cityname bas√©e sur l'index.
        CityName = Worksheets("Cities").Cells(SheetIndex, 1)
        
        For DateIndex = 1 To NumberOfDays
            Worksheets(CityName).Cells(DateIndex + 1, 1) = DateSerial(Year(ReportDate), Month(ReportDate), DateIndex)
        Next DateIndex
    Next SheetIndex
    
End Sub
```

Ce sera une boucle dans une boucle pour pimenter un peu ! üî• 

Je vais vous dire ce que nous faisons ici. Encore une fois, nous parcourons les villes. Mais au lieu de cr√©er une autre feuille de calcul, nous allons maintenant faire autre chose avec la variable `Cityname`.

Dans la boucle √† l'int√©rieur de la boucle (vous voyez toujours o√π je suis maintenant ?) nous imprimons la date sur la feuille, o√π, comme plus t√¥t dans ce tutoriel, nous avons utilis√© la fonction `DateSerial`.

Assurez-vous de supprimer les feuilles avec un nom de ville de votre classeur, ex√©cutez la macro √† nouveau, et vous devriez voir les feuilles de calcul incluant les dates pour le mois.

Notez que les dates seront imprim√©es √† partir de la deuxi√®me ligne, car j'ai inclus +1 pour ma colonne de cellule dans mon code pr√©c√©dent. Pourquoi, pourriez-vous vous demander ? Eh bien, parce que je veux ajouter certains titres sur la premi√®re ligne √† la place, que nous allons ajouter √† la m√™me boucle tout de suite.

Plut√¥t que de d√©finir les cellules en utilisant la propri√©t√© Cells suivie du num√©ro de ligne et de colonne en tant qu'entier, nous pouvons utiliser `Range` suivi de l'emplacement d'une cellule (ou de plusieurs cellules) pour cibler sa valeur et d'autres propri√©t√©s.

```vba
    For SheetIndex = 1 To NumberOfCities
        'D√©finition de la variable cityname bas√©e sur l'index.
        CityName = Worksheets("Cities").Cells(SheetIndex, 1)
        
        For DateIndex = 1 To NumberOfDays
            Worksheets(CityName).Cells(DateIndex + 1, 1) = DateSerial(Year(ReportDate), Month(ReportDate), DateIndex)
        Next DateIndex
        
        Worksheets(CityName).Range("B1") = "Minimum"
        Worksheets(CityName).Range("C1") = "Moyenne"
        Worksheets(CityName).Range("D1") = "Maximum"
    Next SheetIndex
```

Dans l'exemple ci-dessus, j'ai ajout√© les en-t√™tes pour notre tableau, qui seront maintenant imprim√©s sur chaque feuille de calcul gr√¢ce √† notre boucle. 

## Comment utiliser des tableaux en VBA

Vous avez d√©j√† entendu parler de DRY ? Ne vous r√©p√©tez pas ! Malheureusement, c'est exactement ce que nous faisons ici. 

Une alternative pourrait √™tre de stocker les trois en-t√™tes dans un tableau et de les parcourir pour obtenir le m√™me r√©sultat. Est-ce vraiment n√©cessaire pour trois √©l√©ments ? C'est discutable, pour √™tre honn√™te. Mais ci-dessous, vous trouverez mon exemple de code tel que d√©crit :

```
Sub CreateTemplate()
    'Variable qui contiendra le nom de la ville utilis√© pour nommer une feuille.
    Dim CityName As String
    'Variable qui sera utilis√©e pour suivre l'index de la boucle pour ajouter les dates individuelles pour le mois.
    Dim DateIndex As Integer
    'Variable qui sera utilis√©e pour suivre l'index de la boucle pour ins√©rer les en-t√™tes.
    Dim HeaderIndex As Integer
    'Variant qui stockera les en-t√™tes utilis√©s dans chaque feuille de calcul.
    Dim Headers(2) As Variant
    'Variable qui contiendra le nombre de villes.
    Dim NumberOfCities As Integer
    'Variable qui contiendra le nombre de jours dans le mois choisi
    Dim NumberOfDays As Integer
    'Variable qui contiendra la date entr√©e dans la feuille de calcul Date.
    Dim ReportDate As Date
    'Variable qui sera utilis√©e pour suivre l'index de la boucle pour cr√©er des feuilles de calcul.
    Dim SheetIndex As Integer
    
    Headers(0) = "Minimum"
    Headers(1) = "Moyenne"
    Headers(2) = "Maximum"
    
    NumberOfCities = Worksheets("Cities").UsedRange.Rows.Count
    
    For SheetIndex = 1 To NumberOfCities
        'D√©finition de la variable cityname bas√©e sur l'index.
        CityName = Worksheets("Cities").Cells(SheetIndex, 1)
        'Ajout d'une nouvelle feuille de calcul appel√©e NewSheet au classeur en dernier pour assurer l'ordre alphab√©tique.
        Sheets.Add(After:=Sheets(Sheets.Count)).Name = CityName
    Next SheetIndex
    
    ReportDate = Worksheets("Date").Cells(1, 2)
    NumberOfDays = DaysInMonth(ReportDate)
    
    For SheetIndex = 1 To NumberOfCities
        'D√©finition de la variable cityname bas√©e sur l'index.
        CityName = Worksheets("Cities").Cells(SheetIndex, 1)
        
        For DateIndex = 1 To NumberOfDays
            Worksheets(CityName).Cells(DateIndex + 1, 1) = DateSerial(Year(ReportDate), Month(ReportDate), DateIndex)
        Next DateIndex
        
        For HeaderIndex = 0 To UBound(Headers) - LBound(Headers)
            Worksheets(CityName).Cells(1, HeaderIndex + 2) = Headers(HeaderIndex)
        Next HeaderIndex
        
    Next SheetIndex
    
End Sub
```

Le variant est utilis√© comme un tableau, qui est d√©clar√© l√† o√π vous vous y attendez et les titres sont ins√©r√©s plus tard par index directement apr√®s la d√©claration de la variable. 

Au lieu de fournir les emplacements de cellules individuels, j'ai utilis√© une boucle qui s'ex√©cute de z√©ro au nombre d'√©l√©ments (moins un, car les tableaux en VBA commencent √©galement √† z√©ro). Le titre est ensuite imprim√© sur la feuille, comme dans la situation pr√©c√©dente. 

Vous devrez peut-√™tre ajouter un autre en-t√™te plus tard, auquel cas la derni√®re solution est plus facile. Mais si vous √™tes s√ªr √† cent pour cent de rester avec trois seulement, choisissez ce qui est le plus pratique pour vous. Il est pr√©f√©rable de conna√Ætre les deux m√©thodes apr√®s tout. 

_Cela n'a pas d'importance laquelle des deux options vous choisissez pour le reste de ce tutoriel, tant que vous avez imprim√© les en-t√™tes._

## Comment importer des donn√©es en utilisant VBA

√Ä ce stade, nous avons cr√©√© le mod√®le r√©el, donc nous pouvons entrer des villes et une date dans le classeur. Ensuite, la macro fera le reste pour nous. 

J'aimerais aller plus loin dans ce tutoriel et vous montrer comment d√©clencher une fen√™tre d'explorateur o√π vous pouvez choisir un fichier et importer les donn√©es de ce fichier.

Assurez-vous d'avoir termin√© les √©tapes ci-dessus et d'avoir obtenu les feuilles de calcul pour toutes les villes. Commen√ßons une nouvelle sous-routine et d√©finissons les variables suivantes en haut :

```
Sub ImportData()
    Dim FileLocation As String
    Dim WorksheetTitle As String
    
    WorksheetTitle = ActiveSheet.Name
End Sub
```

Nous avons besoin de la premi√®re variable pour stocker l'emplacement du fichier o√π nous allons importer les donn√©es sur votre appareil. Nous utilisons la seconde pour stocker le titre de la feuille actuellement s√©lectionn√©e par l'utilisateur.

Ajoutez le code suivant √† la macro, juste en dessous de la ligne o√π nous d√©finissons `WorksheetTitle` √©gal au nom de la feuille (mais toujours avant End Sub) :

```
FileLocation = Application.GetOpenFilename
If FileLocation = "False" Then
    Beep
    Exit Sub
End If

Application.ScreenUpdating = False
Set ImportWorkbook = Workbooks.Open(Filename:=FileLocation)

Debug.Print ImportWorkbook.Worksheets(1).Range("B1")

ImportWorkbook.Close
Application.ScreenUpdating = True
```

Cela peut sembler un peu cryptique au premier abord, mais ne devrait pas √™tre trop difficile √† comprendre. La fonction `Application.GetOpenFilename` est ce qui d√©clenche une fen√™tre d'explorateur de fichiers et retourne un emplacement, qui est ensuite stock√© dans la variable d√©finie pr√©c√©demment.

Si vous d√©cidez de ne pas s√©lectionner de fichier, ce qui signifie que si `FileLocation` = "False", vous entendrez le son iconique du bip (oui, Excel vient aussi avec des sons g√©niaux !üòç) et la macro se terminera ici.

Si c'est vrai, nous continuerons avec la macro, qui met d'abord fin aux mises √† jour de l'√©cran d'Excel. Cela aide √† s'assurer que tout se d√©roulera sans accroc. 

Pour cette macro, vous ne remarquerez peut-√™tre pas la diff√©rence sans cette ligne, mais avec des calculs plus complexes, vous le ferez. Croyez-moi.

## Comment travailler avec des objets en VBA

Apr√®s cela, les donn√©es incluses dans le fichier situ√© o√π la variable FileLocation est stock√©e dans la constante `ImportWorkbook`. 

Notez que nous utilisons le mot-cl√© `Set` qui est principalement utilis√© pour les objets, comme une importation d'un classeur. 

Pour tester si nous sommes capables d'extraire la date du fichier, nous utilisons √† nouveau `Debug.Print` pour imprimer la ville, qui est situ√©e dans la cellule B1 des fichiers disponibles via le lien mentionn√© au d√©but de ce tutoriel.

Si vous importez le fichier `Data_Amsterdam`, vous devriez voir Amsterdam imprim√© dans votre Fen√™tre Imm√©diate. Apr√®s la fermeture du fichier, Excel se comportera normalement √† nouveau. Nous pouvons facilement coller le nom de la ville (juste imprim√©) dans notre propre mod√®le en rempla√ßant le `Debug.Print` par ce qui suit :

```vba
ThisWorkbook.Worksheets(WorksheetTitle).Range("A1") = ImportWorkbook.Worksheets(1).Range("B1")

'√âquivalent √† ce qui suit.
ThisWorkbook.Worksheets(WorksheetTitle).Cells(1, 1) = ImportWorkbook.Worksheets(1).Range(1, 2)
```

Notez que nous pouvons maintenant utiliser notre variable `ImportWorkbook` pour obtenir les donn√©es du classeur import√©. Mais nous pouvons √©galement utiliser `ThisWorkbook` (nom de mot-cl√© prot√©g√© par VBA) pour obtenir les propri√©t√©s de notre propre mod√®le √† la place. 

Si nous ex√©cutons maintenant cette macro et importons `Data_Amsterdam`, la ville devrait maintenant √™tre imprim√©e dans le coin sup√©rieur gauche de votre feuille de calcul Amsterdam.

## Comment utiliser des dictionnaires en VBA

Comme promis, il est maintenant temps d'obtenir les temp√©ratures r√©elles. Vous pouvez utiliser une boucle et ins√©rer les nombres comme d√©montr√© ci-dessus, mais je me sens fantaisiste aujourd'hui et j'aimerais vous montrer comment nous pouvons faire cela en utilisant un dictionnaire.

Encore une fois, comme le tableau plus t√¥t dans ce tutoriel, cette approche peut √™tre un peu redondante pour cette situation. Mais si vous devez travailler avec de grandes quantit√©s de donn√©es, cela sera utile.

La premi√®re chose que nous devons faire est de d√©finir plusieurs nouvelles variables ici :

```
Dim DataIndex As Integer
Dim DaysIndex As Integer
Dim FileLocation As String
Dim Headers(2) As Variant
Dim WorksheetTitle As String
Set TemperaturesDict = CreateObject("Scripting.Dictionary")

Headers(0) = "Maximum"
Headers(1) = "Minimum"
Headers(2) = "Moyenne"
```

Les deux index en haut suivront l'index des boucles que nous utiliserons plus tard. J'ai copi√© la d√©claration des en-t√™tes et des variables de notre macro pr√©c√©dente. 

_Oui, nous aurions pu les d√©finir comme une variable globale, mais c'est un autre sujet pour un autre jour._ 

Notez que j'ai chang√© l'ordre, car c'est ainsi qu'ils sont affich√©s dans le fichier import√©.

Un dictionnaire est √©galement un objet, que nous d√©clarerons donc en utilisant le mot-cl√© `Set` directement en dessous de nos variables.

Mais avant de pouvoir utiliser un dictionnaire de cette mani√®re, assurez-vous que vous avez coch√© Microsoft Scripting Runtime (comme vous pouvez le voir dans la Figure 6 ci-dessus), que vous pouvez trouver sous Outils puis R√©f√©rences dans votre √©diteur.

![Image](https://www.freecodecamp.org/news/content/images/2021/07/image-91.png)
_Figure 06 : R√©f√©rences Visual Basic_

Auparavant, je vous ai montr√© comment copier une valeur d'une seule cellule d'un classeur √† un second. Maintenant, plut√¥t que de copier des valeurs individuelles, je veux les stocker toutes ensemble dans un dictionnaire avant de continuer et de les coller.

```vba
    For DaysIndex = 11 To ImportWorkbook.Worksheets(1).UsedRange.Rows.Count
        Set DataDict = CreateObject("Scripting.Dictionary")
        For DataIndex = 0 To 2
            DataDict.Add Headers(DataIndex), ImportWorkbook.Worksheets(1).Cells(DaysIndex, DataIndex + 2)
        Next DataIndex
        TemperaturesDict.Add DaysIndex, DataDict
    Next DaysIndex
```

Cela pourrait √™tre la partie la plus difficile du tutoriel, que je vais essayer d'expliquer aussi clairement que possible. 

Nous commen√ßons par une boucle comme nous l'avons utilis√©e de nombreuses fois auparavant dans ce tutoriel ‚Äì jusqu'√† pr√©sent, tout va bien. La boucle commence √† onze et se termine lorsque le nombre de lignes utilis√©es dans le classeur import√© est atteint.

Ensuite, je d√©finis un nouveau dictionnaire. Pourquoi ? Dans la plupart des autres langages, il est possible d'ajouter de nouvelles paires cl√©-valeur en utilisant la notation par points ‚Äì mais pas en VBA. Afin de soi-disant imbriquer (ajouter un dictionnaire dans un dictionnaire), vous devrez cr√©er un nouveau dictionnaire (interne) puis l'ajouter √† votre autre dictionnaire (externe).

Ce dictionnaire dans la boucle sera donc d'abord peupl√© avec les diff√©rentes temp√©ratures disponibles, √† savoir minimum, moyenne et maximum. 

Pour ce faire, j'utiliserai le tableau d'en-t√™tes comme discut√© ci-dessus et je parcourrai ces valeurs. Vous ajoutez une paire cl√©-valeur en utilisant le nom du dictionnaire suivi de l'appel de la m√©thode `.Add`, la cl√© que vous souhaitez utiliser, une virgule, puis sa valeur :

```vba
DictName.Add Key, Value
```

C'est ce qui sera entr√© apr√®s la premi√®re it√©ration lorsque vous importez le fichier `Data_Amsterdam` :

```
Maximum: 22.105547
Minimum: 14.385546	
Moyenne: 18.25388	

```

Ce dictionnaire est ensuite ajout√© au dictionnaire 'principal' appel√© `TemperaturesDict` ‚Äì ce qui signifie que nous aurons maintenant quelque chose comme ceci :

```
10: 
    Maximum: 22.105547
    Minimum: 14.385546	
    Moyenne: 18.25388	

```

Nous faisons de m√™me pour tous les autres jours et types de temp√©ratures, jusqu'√† ce que le fichier soit compl√®tement extrait et que nous soyons pr√™ts √† ins√©rer ces informations dans notre mod√®le. Mais alors quoi ?

```vba
    Headers(0) = "Minimum"
    Headers(1) = "Moyenne"
    Headers(2) = "Maximum"
    
    For DaysIndex = 2 To ThisWorkbook.Worksheets(WorksheetTitle).UsedRange.Rows.Count
        If TemperaturesDict.Exists(DaysIndex - 1) Then
            For DataIndex = 0 To 2
                ThisWorkbook.Worksheets(WorksheetTitle).Cells(DaysIndex, DataIndex + 2) = TemperaturesDict(DaysIndex - 1)(Headers(DataIndex))
            Next DataIndex
        End If
    Next DaysIndex
```

Comme discut√© ci-dessus, le fichier import√© a un autre ordre. Mais puisque nous utilisons un dictionnaire, nous pouvons maintenant facilement r√©organiser le tableau des en-t√™tes et l'afficher comme nous l'avions initialement pr√©vu dans notre mod√®le, car il recherchera la cl√© (#winning).

Nous utilisons ensuite une boucle √† l'ancienne pour parcourir tous les jours que nous avons dans notre mod√®le. La boucle commence √† deux, car nous avons d√©fini nos en-t√™tes sur la premi√®re ligne. Commencer √† l'index un les √©crasera, ce que nous ne voulons pas. La boucle se termine apr√®s que nous avons eu toutes les lignes (c'est-√†-dire, tous les jours).

Puisque seul un nombre limit√© de jours sont inclus dans le fichier import√©, nous devons v√©rifier si le jour √©gal √† l'index existe r√©ellement dans notre dictionnaire. 

Nous faisons cela avec une instruction if, que vous pourriez reconna√Ætre d'autres langages de programmation. Il y a un moins un puisque notre boucle commence √† deux, bien que les jours du mois commencent toujours √† un. Il en va de m√™me pour le moins un utilis√© plus tard dans ce bloc de code.

Si les donn√©es existent r√©ellement dans notre tableau, nous les ins√©rerons dans notre mod√®le. Ensuite, la ligne est d√©finie par l'index de la boucle et la colonne provient de `DataIndex`. 

Finalement, nous obtenons le code suivant pour notre deuxi√®me macro :

```
Sub ImportData()
    Dim DataIndex As Integer
    Dim DaysIndex As Integer
    Dim FileLocation As String
    'Variant qui stockera les en-t√™tes utilis√©s dans chaque feuille de calcul.
    Dim Headers(2) As Variant
    Dim WorksheetTitle As String
    Set TemperaturesDict = CreateObject("Scripting.Dictionary")
    
    Headers(0) = "Maximum"
    Headers(1) = "Minimum"
    Headers(2) = "Moyenne"
    
    WorksheetTitle = ActiveSheet.Name
    
    FileLocation = Application.GetOpenFilename
    If FileLocation = "False" Then
        Beep
        Exit Sub
    End If
    
    Application.ScreenUpdating = False
    Set ImportWorkbook = Workbooks.Open(Filename:=FileLocation)
    
    For DaysIndex = 11 To ImportWorkbook.Worksheets(1).UsedRange.Rows.Count
        Set DataDict = CreateObject("Scripting.Dictionary")
        For DataIndex = 0 To 2
            DataDict.Add Headers(DataIndex), ImportWorkbook.Worksheets(1).Cells(DaysIndex, DataIndex + 2)
        Next DataIndex
        TemperaturesDict.Add DaysIndex, DataDict
    Next DaysIndex
    
    Headers(0) = "Minimum"
    Headers(1) = "Moyenne"
    Headers(2) = "Maximum"
    
    For DaysIndex = 2 To ThisWorkbook.Worksheets(WorksheetTitle).UsedRange.Rows.Count
        If TemperaturesDict.Exists(DaysIndex - 1) Then
            For DataIndex = 0 To 2
                ThisWorkbook.Worksheets(WorksheetTitle).Cells(DaysIndex, DataIndex + 2) = TemperaturesDict(DaysIndex - 1)(Headers(DataIndex))
            Next DataIndex
        End If
    Next DaysIndex
    
    ImportWorkbook.Close
    Application.ScreenUpdating = True

End Sub
```

Lorsque vous ex√©cutez maintenant ce code, vous devriez voir les temp√©ratures √™tre import√©es du fichier de donn√©es vers le mod√®le. 

Notez que nous avons √©galement chang√© l'ordre dans lequel elles viennent √† l'origine ‚Äì commen√ßant maintenant par le minimum et se terminant par le maximum. 

## Conclusion

F√©licitations ! Vous avez maintenant cr√©√© un mod√®le que vous pouvez facilement remplir avec des donn√©es, tout en utilisant des macros.

Merci d'avoir lu :) Si vous avez des questions sur ce tutoriel ou une autre impl√©mentation VBA, n'h√©sitez pas √† m'envoyer un message direct sur Twitter afin que je puisse vous aider.