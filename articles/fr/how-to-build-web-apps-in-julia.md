---
title: Comment construire votre premi√®re application Web en Julia avec Genie.jl üßû‚Äç‚ôÇÔ∏è
subtitle: ''
author: freeCodeCamp
co_authors: []
series: null
date: '2022-02-01T21:33:45.000Z'
originalURL: https://freecodecamp.org/news/how-to-build-web-apps-in-julia
coverImage: https://www.freecodecamp.org/news/content/images/2022/01/Web-applications-in-Julia.png
tags:
- name: Julialang
  slug: julialang
- name: Julia
  slug: julia
- name: Web Applications
  slug: web-applications
- name: Web Development
  slug: web-development
seo_title: Comment construire votre premi√®re application Web en Julia avec Genie.jl
  üßû‚Äç‚ôÇÔ∏è
seo_desc: "By Logan Kilpatrick\nJulia is a high-level, dynamic, and open-source programming\
  \ language. It's designed to be as easy to use as Python while remaining as performant\
  \ as C or C++. \nMany early use cases for Julia were in the scientific domains where\
  \ mas..."
---

Par Logan Kilpatrick

Julia est un langage de programmation haut niveau, dynamique et open-source. Il est con√ßu pour √™tre aussi facile √† utiliser que Python tout en restant aussi performant que C ou C++. 

De nombreux premiers cas d'utilisation de Julia √©taient dans les domaines scientifiques o√π un traitement computationnel massif √©tait et est encore requis. Mais √† mesure que le langage continue de cro√Ætre, de plus en plus de cas d'utilisation gagnent en popularit√© (indice : d√©veloppement web). 

Si vous √™tes totalement nouveau dans Julia et que vous souhaitez ma√Ætriser la syntaxe avant de vous lancer dans la cr√©ation de votre premi√®re application web, [consultez cet article sur freeCodeCamp](https://www.freecodecamp.org/news/learn-julia-programming-language/).

Il couvre les bases, comment installer Julia, les √©tapes pour installer des packages, et bien plus encore ! 

Nous allons nous concentrer dans ce tutoriel sur toutes les √©tapes n√©cessaires pour construire votre premi√®re application web en Julia √† partir de z√©ro. Alors commen√ßons par consulter le site web de Genie : [https://genieframework.com](https://genieframework.com).

## Qu'est-ce que Genie.jl ? üß†

Genie est un framework web moderne et hautement productif √©crit en Julia. Selon les propres mots du projet :

> Genie est un framework web full-stack qui fournit un flux de travail rationalis√© et efficace pour d√©velopper des applications web modernes. Il s'appuie sur les forces de Julia (haut niveau, haute performance, dynamique, compil√© JIT), exposant une API riche et un ensemble d'outils puissant pour un d√©veloppement web productif.

Genie est tr√®s similaire au [Projet Django](https://www.djangoproject.com) en ce sens que Genie est plus qu'un simple framework. Au lieu de cela, il s'agit d'un √©cosyst√®me entier avec des extensions et autres. 

Mais pourquoi avons-nous besoin de Genie ? La r√©ponse simple est que, √† mesure que Julia continue de gagner en popularit√©, de plus en plus de d√©veloppeurs cherchent √† exploiter Julia dans toute leur stack. Genie fournit la capacit√© de d√©ployer des sites web avec du code Julia s'ex√©cutant c√¥t√© serveur afin que vous puissiez faire des choses comme d√©ployer des mod√®les de machine learning dans le cadre de votre application Genie.

Avant de plonger dans la prise en main de Genie, vous pourriez vouloir consulter une application Genie d√©ploy√©e en direct pour avoir une id√©e de ce qui est possible : [https://pkgs.genieframework.com](https://pkgs.genieframework.com). 

Ce projet est une ressource communautaire o√π vous pouvez interroger le nombre de t√©l√©chargements de packages pendant une certaine p√©riode pour un package sp√©cifique. Tapez "genie" pour voir le nombre de t√©l√©chargements quotidiens.

Vous pourriez √©galement √™tre int√©ress√© √† en apprendre davantage sur d'autres frameworks de d√©veloppement GUI et web en Julia. Pour en savoir plus sur l'√©cosyst√®me, [consultez cet article]( https://towardsdatascience.com/6-julia-frameworks-to-create-desktop-guis-and-web-apps-9ae1a941f115).

## Comment installer Genie ‚§¥Ô∏è

Pour installer Genie, tout ce que nous avons √† faire est d'ouvrir le REPL de Julia et de taper `] add Genie`. Cela s'occupera de tout ce dont vous avez besoin. Si tout fonctionne, vous devriez pouvoir faire :

```julia
julia> using Genie

```

sans aucun probl√®me. Vous √™tes maintenant pr√™t √† commencer √† essayer Genie.

## Comment mapper les URL aux fonctions Julia üéØ

Une partie centrale du framework Genie est l'id√©e d'un routeur. Les routeurs prennent l'action de l'utilisateur de visiter une URL sp√©cifique et l'associent √† l'appel d'une fonction Julia.

Regardons un exemple simple de cela. Dans le REPL, tapez ce qui suit :

```julia
julia> using Genie, Genie.Router

julia> route("/hello") do
           "Hello freeCodeCamp"
       end
[GET] /hello => #5 | :get_hello
```

Dans cet exemple, nous avons d√©fini l'URL "/hello" pour retourner le texte "Hello freeCodeCamp". Nous pouvons v√©rifier que cela fonctionne en d√©marrant le serveur :

```julia
julia> up() # start server
‚îå Info: 
‚îî Web Server starting at http://127.0.0.1:8000 
Genie.AppServer.ServersCollection(Task (runnable) @0x000000011c5c5bb0, nothing)
```

Maintenant que le serveur est en cours d'ex√©cution, nous pouvons visiter [`http://127.0.0.1:8000`](http://127.0.0.1:8000) dans notre navigateur. Vous remarquerez que nous obtenons une page 404, ce qui est attendu puisque la seule route que nous avons d√©finie √©tait "/hello". Alors ajoutons cela √† l'URL et voyons ce que nous obtenons :

![Fen√™tre du navigateur montrant rien d'autre que le texte "Hello freeCodeCamp"](https://www.freecodecamp.org/news/content/images/2022/01/Screen-Shot-2022-01-29-at-8.25.53-AM.png)

Et voil√† ! Notre premi√®re √©tape vers la construction d'une application web enti√®rement fonctionnelle est termin√©e. Nous pouvons √©galement confirmer que la page se charge correctement en v√©rifiant le REPL qui montre ceci :

```julia
julia> ‚îå Error: GET / 404
‚îî @ Genie.Router ~/.julia/packages/Genie/UxbVJ/src/Router.jl:163
‚îå Error: GET /favicon.ico 404
‚îî @ Genie.Router ~/.julia/packages/Genie/UxbVJ/src/Router.jl:163
[ Info: GET /hello 200
```

Nous voyons la premi√®re tentative o√π le r√©sultat √©tait une erreur 404 et lors de la deuxi√®me tentative o√π nous avons r√©ussi √† obtenir la r√©ponse (le message 200 signifie que tout est correct).

Maintenant que nous avons un exemple de base qui fonctionne, essayons de construire quelque chose de plus approfondi. 

Pour ce faire, nous allons cr√©er un nouveau fichier. J'utiliserai VS Code, mais vous √™tes libre d'utiliser l'IDE que vous trouvez utile. Avant de regarder le prochain morceau de code, nous devons nous assurer d'arr√™ter le serveur en tapant `down()` dans le REPL. 

D'accord, passons √† l'exemple suivant :

```julia
using Genie, Genie.Router
using Genie.Renderer, Genie.Renderer.Html, Genie.Renderer.Json

route("/") do
    html("Hey freeCodeCamp")
end

route("/hello.html") do
  html("Hello freeCodeCamp (in html)")
end

route("/hello.json") do
  json("Hi freeCodeCamp (in json)")
end

route("/hello.txt") do
   respond("Hiya freeCodeCamp (in txt format)", :text)
end

# Launch the server on a specific port, 8002
# Run the task asynchronously
up(8002, async = true)
```

Beaucoup de choses se passent dans cet exemple, alors parcourons ce qui se passe. 

Nous commen√ßons par charger les packages que nous voulons. Ensuite, nous d√©finissons 4 routes diff√©rentes. La premi√®re est la route d'index. Ainsi, lorsque l'utilisateur visite [`http://127.0.0.1:8002`](http://127.0.0.1:8002), il verra "Hey freeCodeCamp". Les routes apr√®s l'index mettent en √©vidence le fait que chaque route peut donner une sortie personnalis√©e. Dans certains cas, il peut s'agir de HTML, dans d'autres, il pourrait s'agir de JSON ou de texte brut. 

La derni√®re ligne de cet exemple pr√©sente le code de lancement du serveur. Comme le pr√©cise le commentaire, nous pouvons d√©finir le num√©ro de port sp√©cifique et choisir si nous voulons que les routes s'ex√©cutent de mani√®re asynchrone ou non. Nous avons maintenant cr√©√© avec succ√®s notre premier [Script Genie](https://genieframework.com/docs/tutorials/Getting-Started.html#developingasimplegeniescript) ! 

## Comment cr√©er un service web de base üîÆ

Maintenant que nous avons mis les mains dans le cambouis avec les bases, nous allons maintenant commencer √† nous rapprocher de la construction d'une application web compl√®te. 

Avant d'aller jusqu'au bout, nous allons faire le premier pas qui consiste √† cr√©er un service web de base. Pour ce faire, nous allons entrer dans le REPL et changer notre r√©pertoire actuel pour un r√©pertoire facilement accessible. J'utiliserai mon bureau dans ce tutoriel :

```julia
shell> cd Desktop
/Users/logankilpatrick/Desktop
```

Pour entrer en mode shell comme montr√© ci-dessus, il suffit de taper un ";" dans le REPL. Maintenant que nous avons notre r√©pertoire actif d√©fini sur le bureau dans mon cas, nous allons utiliser la fonction g√©n√©ratrice pratique pour cr√©er le service :

```julia
julia> Genie.newapp_webservice("freeCodeCampApp")

[ Info: Done! New app created at /Users/logankilpatrick/Desktop/freeCodeCampApp
[ Info: Changing active directory to /Users/logankilpatrick/Desktop/freeCodeCampApp
    /var/folders/tc/519vfm453fj_x5bmd8pwx9480000gn/T/jl_bO1R8h/FreeCodeCampApp/Project.toml
[ Info: Project.toml has been generated
[ Info: Installing app dependencies
...
```

La fonction `newapp_webservice` est une fonction tr√®s utile qui cr√©e automatiquement toutes les pi√®ces dont nous avons besoin pour notre premier service web. Maintenant que nous avons un projet cr√©√©, nous devons l'ouvrir dans un IDE (dans mon cas, VS Code). Vous devriez voir ce qui suit si vous ouvrez le bon dossier :

![Image](https://www.freecodecamp.org/news/content/images/2022/01/Screen-Shot-2022-01-30-at-7.39.23-PM.png)

Il y a beaucoup de fichiers cr√©√©s automatiquement pour nous. Le principal que nous allons examiner est `routes.jl` qui est utilis√© pour cr√©er des routes comme nous l'avons fait dans la section ci-dessus. 

La fonction que nous avons appel√©e pour g√©n√©rer ces dossiers d√©marre automatiquement le serveur, alors jetons un rapide coup d'≈ìil √† la page d'accueil existante en visitant [http://127.0.0.1:8000](http://127.0.0.1:8000) :

![Image](https://www.freecodecamp.org/news/content/images/2022/01/Screen-Shot-2022-01-30-at-7.51.16-PM.png)

Comme vous pourriez le remarquer, ma page semble un peu diff√©rente de la v√¥tre car je suis all√© modifier la page `welcome.html` trouv√©e dans le dossier public. 

Comme vous pouvez le voir dans `routes.jl`, lorsque l'utilisateur visite l'URL principale `/`, nous le redirigeons vers la page de bienvenue. Nous pouvons ajouter des routes suppl√©mentaires comme nous l'avons fait dans la section ci-dessus et √©tendre cela. Vous √™tes libre de faire une pause ici et de jouer un peu. Nous avons d√©j√† un site web assez robuste configur√©.

Si vous jetez un coup d'≈ìil dans certains des autres dossiers comme `config/env`, vous verrez des d√©tails sur la d√©finition du port, de l'URL de l'h√¥te et d'autres param√®tres pertinents. Encore une fois, n'h√©sitez pas √† jouer avec cela, mais nous n'entrerons pas dans tous les d√©tails de ces fichiers dans ce tutoriel. 

Avant de plonger dans le prochain sujet, jetons un coup d'≈ìil √† quelques-uns des autres fichiers g√©n√©r√©s pour notre service web de base :

* Le dossier public contient tous les fichiers front-end (HTML et CSS)
* Le dossier `src` contient le point d'entr√©e du service web (dans mon cas `freeCodeCampApp.jl`)
* bin contient quelques d√©pendances suppl√©mentaires que nous ignorerons √† nouveau
* Manifest.toml et Project.toml sont les fichiers cl√©s de Julia qui nous permettent de maintenir nos d√©pendances Julia. Lorsque vous avez cr√©√© le service web, le script a automatiquement activ√© votre environnement de projet actuel (qui est l'application que nous venons de cr√©er). Vous pouvez v√©rifier cela en tapant "]" dans le REPL, ce qui montrera l'espace actif en bleu :

![Image](https://www.freecodecamp.org/news/content/images/2022/01/Screen-Shot-2022-01-30-at-7.59.49-PM.png)

Cela signifie simplement que si nous essayons d'ajouter un package, il l'ajoutera au fichier de projet et de manifest sp√©cifique √† ce projet, au lieu de celui partag√© globalement.

## Comment cr√©er une application web enti√®rement fonctionnelle avec une base de donn√©es üìΩ

Maintenant que nous avons explor√© les bases, nous allons plonger dans une application web compl√®te. Encore une fois, Genie fournit quelques fonctions utiles pour nous aider √† d√©marrer. Avant de la cr√©er, nous devrons revenir au bureau :

```julia
shell> pwd
/Users/logankilpatrick/Desktop/freeCodeCampApp

shell> cd ..
/Users/logankilpatrick/Desktop

shell> 
```

Rappelez-vous, vous pouvez taper `;` pour entrer en mode shell et backspace pour quitter le mode shell. Maintenant, cr√©ons l'application :

```julia
julia> Genie.newapp_mvc(Genie.newapp_mvc("freeCodeCampMVC"))
   Resolving package versions...
   ...
```

Vous serez invit√© √† choisir un backend de base de donn√©es. Pour cet exemple, nous utiliserons SQLite :

![Image](https://www.freecodecamp.org/news/content/images/2022/01/Screen-Shot-2022-01-30-at-8.08.31-PM.png)

Si vous souhaitez utiliser un autre backend de base de donn√©es, n'h√©sitez pas √† le faire √©galement. Mais notez que vous devrez cr√©er le fichier de base de donn√©es automatiquement. Genie ne cr√©e qu'un fichier SQLite pour vous. 

Nous avons maintenant une application MVC cr√©√©e. Mais vous pourriez vous demander, qu'est-ce qu'un MVC ? 

Le paradigme Mod√®le-Vue-Contr√¥leur est tr√®s courant dans le d√©veloppement d'applications. Dans l'int√©r√™t de ne pas entrer dans les d√©tails, je vous [renvoie √† cet article](https://www.freecodecamp.org/news/mvc-architecture-what-is-a-model-view-controller-framework/) o√π vous pouvez lire les d√©tails. Du point de vue des d√©veloppeurs, il n'y a pas beaucoup d'impact. 

Tout comme nous l'avons fait lorsque nous avons cr√©√© le dernier projet, nous devons l'ouvrir √† nouveau dans l'IDE :

![Image](https://www.freecodecamp.org/news/content/images/2022/02/Screen-Shot-2022-02-01-at-6.44.21-AM.png)

Encore une fois, nous verrons beaucoup des m√™mes √©l√©ments qu'auparavant avec l'ajout du dossier `app` qui contiendra beaucoup de code critique. Nous pouvons voir √† quoi ressemble le nouveau projet en tapant :

```julia
julia> loadapp()

julia> up()
```

et en naviguant ensuite vers : [http://127.0.0.1:8000](http://127.0.0.1:8000).

Ensuite, nous devrons connecter notre base de donn√©es √† l'application web que nous avons cr√©√©e. Pour ce faire, rendez-vous dans `db/connection.yml` et modifiez la section suivante :

```yml
env: ENV["GENIE_ENV"]

dev:
  adapter: SQLite
  database: db/freeCodeCamp_courses.sqlite
```

Vous pouvez laisser les autres champs vides pour l'instant. Ensuite, nous devons ex√©cuter :

```julia
julia> include(joinpath("config", "initializers", "searchlight.jl"))
```

ce qui chargera la configuration de la base de donn√©es. Ensuite, nous allons continuer √† configurer la base de donn√©es de mani√®re √† pouvoir sauvegarder les donn√©es de notre application dans un stockage persistant.

Nous commen√ßons ce processus en cr√©ant une nouvelle ressource :

```julia
julia> Genie.newresource("course")
```

Une fois que nous avons d√©fini une ressource, l'√©tape suivante consiste √† aller modifier la table des migrations de la base de donn√©es qui se trouve dans `db/migrations/2022020115190055_create_table_courses.jl` dans mon cas. 

Par d√©faut, la table est d√©j√† remplie avec un texte de remplissage bas√© sur les derni√®res commandes que nous avons ex√©cut√©es. Elle devrait ressembler √† quelque chose comme ceci :

![Image](https://www.freecodecamp.org/news/content/images/2022/02/Screen-Shot-2022-02-01-at-7.22.35-AM.png)

Nous allons modifier le fichier pour qu'il corresponde au sch√©ma sp√©cifique que nous voulons. Cela d√©pendra enti√®rement de l'application elle-m√™me. Puisque je cr√©e des cours sur ce site, je vais entrer tous les d√©tails des cours comme suit :

```julia
module CreateTableCourses

import SearchLight.Migrations: create_table, column, columns, pk, add_index, drop_table, add_indices

function up()
  create_table(:courses) do
    [
      pk()
      column(:title, :string, limit = 200)
      column(:authors, :string, limit = 250)
      column(:year, :integer, limit = 4)
      column(:rating, :string, limit = 10)
      column(:categories, :string, limit = 100)
      column(:description, :string, limit = 1_000)
      column(:cost, :float, limit = 1000)
    ]
  end

  add_index(:courses, :title)
  add_index(:courses, :authors)
  add_index(:courses, :categories)
  add_index(:courses, :description)

end

function down()
  drop_table(:courses)
end

end
```

Encore une fois, ceux-ci sont arbitraires et peuvent √™tre ce que vous voulez qu'ils soient. 

Il est int√©ressant de noter que l'ajout de l'index est facultatif. La raison pour laquelle vous l'ajouteriez est qu'il acc√©l√®re les requ√™tes, mais il y a d'autres compromis et vous ne pouvez pas r√©ellement charger toutes les colonnes en tant qu'index. Vous pouvez en lire plus sur certains de ces compromis [ici](https://stackoverflow.com/questions/5447987/why-cant-i-simply-add-an-index-that-includes-all-columns/5448055#5448055) et [ici](https://stackoverflow.com/questions/107132/what-columns-generally-make-good-indexes).

Maintenant que nous avons mis √† jour la table de la base de donn√©es, nous devons propager ces mises √† jour. Pour ce faire, nous allons utiliser `SearchLight.jl` qui fonctionne comme le syst√®me de migration de notre application :

```julia
julia> using SearchLight

julia> SearchLight.Migration.create_migrations_table()
‚îå Info: 2022-02-01 07:37:11 CREATE TABLE `schema_migrations` (
‚îÇ       `version` varchar(30) NOT NULL DEFAULT '',
‚îÇ       PRIMARY KEY (`version`)
‚îî     )
[ Info: 2022-02-01 07:37:11 Created table schema_migrations

julia> SearchLight.Migration.status()
[ Info: 2022-02-01 07:37:20 SELECT version FROM schema_migrations ORDER BY version DESC
|   | Module name & status                     |
|   | File name                                |
|---|------------------------------------------|
|   |                 CreateTableCourses: DOWN |
| 1 | 2022020115190055_create_table_courses.jl |

julia> SearchLight.Migration.last_up()
[ Info: 2022-02-01 07:37:29 SELECT version FROM schema_migrations ORDER BY version DESC
[ Info: 2022-02-01 07:37:29 CREATE TABLE courses (id INTEGER PRIMARY KEY , title TEXT  , authors TEXT  , year INTEGER (4) , rating TEXT  , categories TEXT  , description TEXT  , cost FLOAT (1000) )
[ Info: 2022-02-01 07:37:29 CREATE  INDEX courses__idx_title ON courses (title)
[ Info: 2022-02-01 07:37:29 CREATE  INDEX courses__idx_authors ON courses (authors)
[ Info: 2022-02-01 07:37:29 CREATE  INDEX courses__idx_categories ON courses (categories)
[ Info: 2022-02-01 07:37:29 CREATE  INDEX courses__idx_description ON courses (description)
[ Info: 2022-02-01 07:37:29 INSERT INTO schema_migrations VALUES ('2022020115190055')
[ Info: 2022-02-01 07:37:29 Executed migration CreateTableCourses up
```

Nous avons maintenant termin√© avec succ√®s les migrations. Si vous deviez apporter une modification au sch√©ma, vous devriez relancer les commandes ci-dessus pour que ces modifications de base de donn√©es prennent effet. 

La derni√®re √©tape de ce processus consiste √† d√©finir notre mod√®le. Cela nous permettra de cr√©er des objets dans le code Julia et de les sauvegarder dans la base de donn√©es que nous venons de d√©finir. Nous devons naviguer vers `app/resources/courses/Courses.jl` ou le chemin √©quivalent pour apporter ces derni√®res mises √† jour : 

```julia
module Courses

import SearchLight: AbstractModel, DbId
import Base: @kwdef

export Course

@kwdef mutable struct Course <: AbstractModel
  id::DbId = DbId()
  title::String = ""
  authors::String = ""
  year::Int = 0
  rating::String = ""
  categories::String = ""
  description::String = ""
  cost::Float64 = 0.0
end

end
```

Encore une fois, cela devrait √™tre le m√™me que le contenu que vous avez pr√©c√©demment d√©fini. Pour vous assurer que cela a fonctionn√©, nous pouvons faire :

```julia
julia> using Courses
[ Info: 2022-02-01 07:43:51 Precompiling Courses [top-level]
```

et ensuite essayer de cr√©er un cours via :

```julia

julia> c = Course(title = "Web dev with Genie.jl", authors="Logan Kilpatrick")
Course
| KEY                 | VALUE                 |
|---------------------|-----------------------|
| authors::String     | Logan Kilpatrick      |
| categories::String  |                       |
| cost::Float64       | 0.0                   |
| description::String |                       |
| id::DbId            | NULL                  |
| rating::String      |                       |
| title::String       | Web dev with Genie.jl |
| year::Int64         | 0                     |
```

Nous avons cr√©√© avec succ√®s notre premier objet ! Mais il n'est pas sauvegard√© dans la base de donn√©es tout de suite. Nous pouvons v√©rifier cela en faisant :

```julia
julia> ispersisted(c)
false
```

nous devons donc ex√©cuter :

```julia
julia> save(c)
[ Info: 2022-02-01 07:47:04 INSERT  INTO courses ("title", "authors", "year", "rating", "categories", "description", "cost") VALUES ('Web dev with Genie.jl', 'Logan Kilpatrick', 0, '', '', '', 0.0) 
[ Info: 2022-02-01 07:47:04 ; SELECT CASE WHEN last_insert_rowid() = 0 THEN -1 ELSE last_insert_rowid() END AS LAST_INSERT_ID
true

```

et maintenant le cours est sauvegard√© ! Mais pour vraiment tester cela, nous devons permettre √† l'utilisateur de cr√©er un cours. Retournons √† `routes.jl` et activons cela :

```julia
using Genie, Genie.Router, Genie.Renderer.Html, Genie.Requests
using Courses

form = """
<form action="/" method="POST" enctype="multipart/form-data">
  <input type="text" name="name" value="" placeholder="What's the course name?" />
  <input type="text" name="author" value="" placeholder="Who is the course author?" />

  <input type="submit" value="Submit" />
</form>
"""

route("/") do
  html(form)
end

route("/", method = POST) do
  c = Course(title=postpayload(:name, "Placeholder"), authors=postpayload(:author, "Placeholder"))
  save(c)
  "Course titled $(c.title) created successfully!"
end
```

Nous avons commenc√© par d√©finir un simple formulaire HTML (rien de nouveau ou d'excitant ici), puis, nous avons fait en sorte que la route par d√©faut `/` rende le formulaire HTML. Enfin, nous cr√©ons une autre route pour l'URL `/`, mais sp√©cifiquement pour la m√©thode POST. √Ä l'int√©rieur de cette route, nous cr√©ons un nouveau cours en extrayant les informations que nous voulons du formulaire √† partir de la charge utile via `postpayload`. 

Vous pouvez essayer cela en naviguant vers : [http://127.0.0.1:8000](http://127.0.0.1:8000)

![Image](https://www.freecodecamp.org/news/content/images/2022/02/Screen-Shot-2022-02-01-at-8.11.38-AM.png)

Vous pouvez essayer d'entrer certains des d√©tails et ensuite appuyer sur soumettre. Pour vous assurer que les soumissions ont fonctionn√©, vous pouvez faire :

```julia
julia> all(Course)
[ Info: 2022-02-01 08:10:19 SELECT "courses"."id" AS "courses_id", "courses"."title" AS "courses_title", "courses"."authors" AS "courses_authors", "courses"."year" AS "courses_year", "courses"."rating" AS "courses_rating", "courses"."categories" AS "courses_categories", "courses"."description" AS "courses_description", "courses"."cost" AS "courses_cost" FROM "courses" ORDER BY courses.id ASC
‚îå Warning: 2022-02-01 08:10:19 Unsupported SQLite declared type INTEGER (4), falling back to Int64 type
‚îî @ SQLite ~/.julia/packages/SQLite/aDggE/src/SQLite.jl:416
‚îå Warning: 2022-02-01 08:10:19 Unsupported SQLite declared type FLOAT (1000), falling back to Float64 type
‚îî @ SQLite ~/.julia/packages/SQLite/aDggE/src/SQLite.jl:416
3-element Vector{Course}:
 Course
| KEY                 | VALUE                 |
|---------------------|-----------------------|
| authors::String     | Logan Kilpatrick      |
| categories::String  |                       |
| cost::Float64       | 0.0                   |
| description::String |                       |
| id::DbId            | 1                     |
| rating::String      |                       |
| title::String       | Web dev with Genie.jl |
| year::Int64         | 0                     |

 Course
| KEY                 | VALUE       |
|---------------------|-------------|
| authors::String     | Logan K     |
| categories::String  |             |
| cost::Float64       | 0.0         |
| description::String |             |
| id::DbId            | 2           |
| rating::String      |             |
| title::String       | Test course |
| year::Int64         | 0           |
```

ce qui devrait montrer que les entr√©es ont √©t√© sauvegard√©es dans la base de donn√©es.

## Conclusion üéÅ

Wow, c'√©tait beaucoup. Nous avons couvert une quantit√© √©norme de terrain dans ce seul tutoriel. 

Cela dit, il y a encore plus √† apprendre sur Genie. Je vous sugg√®re vivement de consulter [la documentation ici](https://genieframework.com/docs/tutorials/Overview.html), qui contient de nombreux autres tutoriels sur des sujets comme les API REST, l'authentification, et bien plus encore. 

## Obtenir de l'aide avec Genie.jl üö®

Si vous rencontrez des probl√®mes avec ce tutoriel ou lors de l'utilisation de Genie, veuillez poser une question sur Stack Overflow avec les tags `genie.jl` et `julia` ou sur le [Julia Discourse](https://discourse.julialang.org). Apr√®s cela, n'h√©sitez pas √† tweeter le lien vers la question √† mon attention et je ferai de mon mieux pour aider : [https://twitter.com/OfficialLoganK](https://twitter.com/OfficialLoganK).